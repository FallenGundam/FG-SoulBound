# name: soulbound
# version 1.1.2
# date: 2023-3-21
# require: skript, skript-reflect, ItemNBTAPI
# hook: MythicDropsProtect

options:
	system: &f[&3&lSoulBound&f]
	bind_aliases1: &c靈魂綁定&a
	bind_aliases2: &4強制綁定&b
	bind_tag: &a使用後自動綁定
	bind_price: 500
	


import:
	de.tr7zw.nbtapi.NBT
	de.tr7zw.nbtapi.NBTItem

#> 更新綁定，如果玩家改名可使用此方法更新綁定lore
function SB_bindupdate(p:player,i:item) :: item:
	if SB_ItemIsBinded({_i}) = true:
		set {_nbt} to new NBTItem({_i})
		if {_nbt}.hasTag("soulbound"):
			set {_owner} to {_nbt}.getString("soulbound")
			if uuid of {_p} = {_owner}:
				set {_l::*} to lore of {_i}
				set {_index} to size of {_l::*}
				loop size of {_l::*} times:
					if {_l::%{_index}%} contains "{@bind_aliases1}" or "{@bind_aliases2}":
						set {_v::*} to {_l::%{_index}%} split at " "
						set {_beforename} to {_v::2}
						if name of {_p} != {_beforename}:
							set line {_index} of lore of {_i} to "%{_v::1}% %name of {_p}%"
							send "{@system} &a成功更新你的物品綁定" to {_p}
						else:
							send "{@system} &c你的物品不需要更新綁定" to {_p}
							stop loop
	else:
		send "{@system} &c該物品沒有綁定" to {_p}
	return {_i}
#> 物品是否已綁定
function SB_ItemIsBinded(i:item) :: boolean:
	if "%lore of {_i}%" contains "{@bind_aliases1}" or "{@bind_aliases2}":
		return true
	return false

#> 判斷物品是否為該玩家的物品
function SB_Bind_conditions(p:player,i:item,msg:boolean) :: boolean:
	if SB_ItemIsBinded({_i}) = true:
		set {_nbt} to new NBTItem({_i})
		if {_nbt}.hasTag("soulbound"):
			set {_owner} to {_nbt}.getString("soulbound")
			if {_owner} = uuid of {_p}:
				return true
			else:
				if {_msg} = true:
					set {_pname} to {_owner} parsed as offlineplayer 
					send "{@system} &c該物品屬於 %{_pname}%" to {_p}
				return false

#> SB_BindItem
#> @param p - player
#> @param i - itemstack
#> @param type - "normal" or "force"
function SB_BindItem(i:item,p:offlineplayer,type:text) :: item:
	set {_nbt} to new NBTItem({_i})
	{_nbt}.setString("soulbound",uuid of {_p})
	set {_newitem} to {_nbt}.getItem()
	if {_type} = "normal":
		add "{@bind_aliases1} %name of {_p}%" to lore of {_newitem}
	else if {_type} = "force":
		add "{@bind_aliases2} %name of {_p}%" to lore of {_newitem}
	return {_newitem}


function SB_unbindItem(i:item,type:text) :: item:
	set {_nbt} to new NBTItem({_i})
	if {_nbt}.hasTag("soulbound"):
		{_nbt}.removeKey("soulbound")
		set {_newitem} to {_nbt}.getItem()
		set {_lore::*} to lore of {_newitem}
		set {_index} to size of {_lore::*}
		loop size of {_lore::*} times:
			if {_type} = "force":
				if {_lore::%{_index}%} contains "{@bind_aliases1}" or "{@bind_aliases2}":
					delete line {_index} of lore of {_newitem}
			else:
				if {_lore::%{_index}%} contains "{@bind_aliases1}":
					delete line {_index} of lore of {_newitem}
			remove 1 from {_index}
		return {_newitem}


command /soulbound [<text>] [<offlineplayer>] [<text>]: 
	permission: sb.admin
	permission message: {@system} &c你沒有使用此指令的權限!
	aliases: sb
	trigger:
		if arg-1 is not set:
			send "&7----------{@system}&7----------"
			send " &f/sb addforce &d將手上武器添加綁定標籤 {@bind_tag}"
			send " &f/sb unbind &d強制解綁手上物品"
			send " &f/sb bind [player] [type] &d綁定手上物品"
			send "&7------------------------------"
		else if arg-1 is "addforce":
			player's tool isn't air
			if SB_ItemIsBinded(player's tool) = false:
				add "{@bind_tag}" to lore of player's tool
				send "{@system} &a添加成功!"
			else:
				send "{@system} &c該物品綁定過了"
		else if arg-1 is "unbind":
			if SB_ItemIsBinded(player's tool) = true:
				set player's tool to SB_unbindItem(tool of player,"force")
				send "{@system} &a成功解除綁定!"
			else:
				send "{@system} &c該物品尚未綁定"
		else if arg-1 is "bind":
			if player's tool isn't air:
				if SB_ItemIsBinded(player's tool) = false:
					if arg-2 isn't set:
						set {_p} to player
					else:
						set {_p} to arg-2 

					if arg-3 isn't set:
						set {_t} to "normal"
					else:
						set {_t} to arg-3
					set player's tool to SB_BindItem(player's tool, {_p}, "%{_t}%")
					send "{@system} &a成功綁定到玩家 %{_p}%!"
				else:
					send "{@system} &c該物品綁定過了"

command soulbound:
	trigger:
		open chest inventory with 3 rows named "&3靈魂綁定介面" to player
		loop 27 times:
			set slot (loop-value)-1 of player's current inventory to black glass pane named ""
		if SB_ItemIsBinded(player's tool) = false:
			set slot 10 of player's current inventory to green wool named "&a綁定此物品"
		else:
			set slot 10 of player's current inventory to blue wool named "&2更新物品綁定"
		set slot 13 of player's current inventory to player's tool
		set slot 16 of player's current inventory to red wool named "&c解除綁定此物品"
on damage:
	if SB_Bind_conditions( attacker, tool of attacker, true ) = false:
		cancel event

on click:
	if SB_Bind_conditions( player, tool of player, true ) = false:
		cancel event

on inventory click:
	if type of event-inventory = player inventory:
		if "%lore of event-slot%" contains "{@bind_event1}":
			cancel event
			set {_item} to event-slot
			set {_line} to size of lore of {_item}
			loop size of lore of {_item} times:
				if line {_line} of lore of {_item} contains "{@bind_event1}":
					delete line {_line} of lore of {_item}
				remove 1 from {_line}
			set event-slot to SB_BindItem( {_item}, player, "force" )
			send "{@system} &a已自動綁定物品"
	if SB_Bind_conditions(player,event-slot,true) = false:
		cancel event
	if name of event-inventory is "&3靈魂綁定介面":
		cancel event
		if type of event-inventory is chest inventory:
			if index of event-slot is 10:
				if SB_ItemIsBinded(player's tool) = false:
					if player's money >= {@bind_price}:
						remove {@bind_price} from player's money
						set player's tool to SB_BindItem(player's tool,player,"normal")
						send "{@system} &a成功綁定物品! 花費了 &e{@bind_price}$"
						play sound "ENTITY_LEASH_KNOT_PLACE" with volume 1 with pitch 0.75 at player
						close player's inventory
					else:
						send "{@system} &c餘額不足"
				else:
					set player's tool to SB_bindupdate( player, tool of player )
			else if index of event-slot is 16:
				if "%lore of player's tool%" contains "{@bind_aliases1}":
					set player's tool to SB_unbindItem(player's tool,"normal")
					send "{@system} &a成功解除綁定!"
					play sound "ENTITY_LEASH_KNOT_PLACE" with volume 1 with pitch 2 at player
					close player's inventory
				else:
					send "{@system} &c該物品不能解除綁定"
on damage of dropped items:
	set {_nbti} to new NBTItem(victim.getItemStack())
	if {_nbti}.hasTag("soulbound"):
		cancel event
